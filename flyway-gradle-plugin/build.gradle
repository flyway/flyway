buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
    }
}

apply plugin: 'groovy'
apply from: 'gradle/publishing.gradle'

def pomProject = new XmlSlurper().parse(file('pom.xml'))

group = pomProject.parent.groupId
version = pomProject.parent.version

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
     mavenLocal()
}

dependencies {
    compile "${group}:flyway-core:${version}"
    compile gradleApi()
    compile localGroovy()
    testCompile 'junit:junit:4.11'
    testCompile 'org.hsqldb:hsqldb:2.3.2'
}

jar {
    from ('..') {
        include 'LICENSE.txt' into('META-INF')
        include 'README.txt' into('META-INF')
    }
}

test.dependsOn cleanTest

test {
    beforeTest { descriptor ->
        println("Running: " + descriptor)
    }
}

task sourceJar(type: Jar) {
    group 'Build'
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

jar.finalizedBy sourceJar

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    sourceJar
    javadocJar
}